buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 113
        versionName '1.1.3'
    }
}

dependencies {
    api 'net.mediavrog:ruli:1.1.0'
    implementation 'com.android.support:support-v4:27.1.1'

    testImplementation 'junit:junit:4.12'
}

group "com.troii"
version android.defaultConfig.versionName
project.archivesBaseName = 'integrated-rating-request'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Integrated Rating Request'
            description 'A polite way to ask for ratings.'
            url 'https://github.com/troii/integrated-rating-request'
            inceptionYear '2018'

            packaging 'aar'
            groupId project.group
            artifactId project.archivesBaseName

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/troii/integrated-rating-request'
                connection 'scm:git@github.com:troii/integrated-rating-request.git'
            }
            developers {
                developer {
                    id "daberni"
                    name "Bernhard Danecker"
                    email "bernhard.danecker@gmail.com"
                }
            }
        }
    }
}

bintray {
    user = project.property("bintray.user")
    key = project.property("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = 'oss'
        name = project.archivesBaseName
        description 'A polite way to ask for ratings.'
        websiteUrl = 'https://github.com/troii/integrated-rating-request'
        vcsUrl = 'https://github.com/troii/integrated-rating-request.git'
        licenses = ['MIT']
        publish = true
        version {
            name = project.version
        }
    }
}
